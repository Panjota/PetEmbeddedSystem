#include <WiFi.h>
#include <PubSubClient.h>

// --- WiFi e MQTT ---
const char* ssid = "JULIE";
const char* password = "Analu2604";
const char* mqttServer = "test.mosquitto.org";
const int mqttPort = 1883;
WiFiClient espClient;
PubSubClient client(espClient);

// --- Pinos ---
#define TRIG_RES 22
#define ECHO_RES 21
#define TRIG_PET 19
#define ECHO_PET 18
#define BOMBA_RELAY 25
#define NIVEL_POTE 34  // GPIO34 (ADC1) para sensor de n√≠vel da tigela

// --- Controle da bomba ---
int tempoBomba = 0;

// --- Visita do pet ---
bool petPresente = false;
unsigned long tempoInicioPresenca = 0;
bool visitaRegistrada = false;

// --- Config do sensor de n√≠vel da tigela ---
int nivelTigela = 0;
const int NIVEL_VAZIO = 700;
const int NIVEL_MINIMO = 1100;
const int NIVEL_ALTO = 2000;

// --- Fun√ß√µes WiFi e MQTT ---
void connectToWiFi() {
  Serial.print("Conectando ao WiFi...");
  WiFi.begin(ssid, password);
  while (WiFi.status() != WL_CONNECTED) {
    delay(500);
    Serial.print(".");
  }
  Serial.println(" conectado!");
}

void connectToMQTT() {
  while (!client.connected()) {
    Serial.print("Conectando ao MQTT...");
    if (client.connect("ESP32WaterDispenser")) {
      Serial.println(" conectado!");
      client.subscribe("petwater/dispensar");
    } else {
      Serial.print(" falhou, rc=");
      Serial.print(client.state());
      Serial.println(" tentando em 5s");
      delay(5000);
    }
  }
}

void mqttCallback(char* topic, byte* payload, unsigned int length) {
  String msg = "";
  for (unsigned int i = 0; i < length; i++) msg += (char)payload[i];
  int tempo = msg.toInt();
  if (tempo > 0) tempoBomba = tempo;
}

float lerUltrassom(int trig, int echo) {
  digitalWrite(trig, LOW);
  delayMicroseconds(2);
  digitalWrite(trig, HIGH);
  delayMicroseconds(10);
  digitalWrite(trig, LOW);
  long duracao = pulseIn(echo, HIGH, 25000);
  return duracao * 0.034 / 2;
}

// Fun√ß√£o para suavizar a leitura do sensor de n√≠vel da tigela
int lerNivelTigelaSuavizado() {
  int total = 0;
  for (int i = 0; i < 10; i++) {
    int leitura = analogRead(NIVEL_POTE);
    // Ignora valores extremos (prov√°vel ru√≠do)
    if (leitura > 50 && leitura < 4090) {
      total += leitura;
    }
    delay(5);
  }
  return total / 10;
}

// Fun√ß√£o para converter leitura bruta em % n√≠vel
int nivelPercentual(int valorSensor) {
  if (valorSensor <= 400) return 0;
  else if (valorSensor <= 1100)
    return map(valorSensor, 400, 1100, 0, 20);
  else if (valorSensor <= 1300)
    return map(valorSensor, 1100, 1300, 20, 50);
  else if (valorSensor <= 1500)
    return map(valorSensor, 1300, 1500, 50, 100);
  else
    return 100;
}

void ativarBomba(int tempo) {
  Serial.println("üíß Liberando √°gua via MQTT...");
  digitalWrite(BOMBA_RELAY, LOW);
  delay(tempo);
  digitalWrite(BOMBA_RELAY, HIGH);
  client.publish("petwater/alerts", "√Ågua liberada");
}

void setup() {
  Serial.begin(115200);

  pinMode(TRIG_RES, OUTPUT);
  pinMode(ECHO_RES, INPUT);
  pinMode(TRIG_PET, OUTPUT);
  pinMode(ECHO_PET, INPUT);
  pinMode(BOMBA_RELAY, OUTPUT);
  digitalWrite(BOMBA_RELAY, HIGH);
  pinMode(NIVEL_POTE, INPUT);

  connectToWiFi();
  client.setServer(mqttServer, mqttPort);
  client.setCallback(mqttCallback);
  connectToMQTT();
}

unsigned long lastPub = 0;
const unsigned long PUBLISH_INTERVAL = 2000;

void loop() {
  if (!client.connected()) connectToMQTT();
  client.loop();

  if (tempoBomba > 0) {
    ativarBomba(tempoBomba);
    tempoBomba = 0;
  }

  float distReservatorio = lerUltrassom(TRIG_RES, ECHO_RES);
  float distPet = lerUltrassom(TRIG_PET, ECHO_PET);
  int leituraBruta = lerNivelTigelaSuavizado();
  nivelTigela = nivelPercentual(leituraBruta);

  unsigned long now = millis();

  // Publica√ß√µes peri√≥dicas
  if (now - lastPub > PUBLISH_INTERVAL) {
    lastPub = now;

    char buffer[16];
    dtostrf(distReservatorio, 5, 2, buffer);
    client.publish("petwater/reservatorio", buffer);

    dtostrf(distPet, 5, 2, buffer);
    client.publish("petwater/pet", buffer);

    Serial.printf("Reservat√≥rio: %.2f cm, Pet: %.2f cm\n", distReservatorio, distPet);
    Serial.print("N√≠vel da tigela (suavizado): ");
    Serial.println(nivelTigela);

    // Publica valor percentual em vez do valor bruto
    itoa(nivelTigela, buffer, 10);
    client.publish("petwater/nivelpote", buffer);  // Agora envia %

    // Publica estado com base no percentual calibrado
    if (nivelTigela == 0) {
      client.publish("petwater/nivelpote/estado", "Vazio");
      client.publish("petwater/alerts", "‚ö†Ô∏è Tigela de √°gua vazia!");
    } else if (nivelTigela > 0 && nivelTigela < 20) {
      client.publish("petwater/nivelpote/estado", "Baixo");
      client.publish("petwater/alerts", "‚ö†Ô∏è Tigela de √°gua com n√≠vel cr√≠tico!");
    } else if (nivelTigela >= 20 && nivelTigela < 50) {
      client.publish("petwater/nivelpote/estado", "M√©dio");
    } else {
      client.publish("petwater/nivelpote/estado", "Alto");
    }
    // Alerta de reservat√≥rio
    if (distReservatorio > 10.0) {
      Serial.println("‚ö†Ô∏è N√≠vel baixo no reservat√≥rio de √°gua!");
      client.publish("petwater/alerts", "‚ö†Ô∏è Reservat√≥rio com n√≠vel baixo!");
    }

  }

  // Monitoramento de visita do pet
  if (distPet < 20.0) {
    if (!petPresente) {
      tempoInicioPresenca = now;
      petPresente = true;
      visitaRegistrada = false;
    } else if (!visitaRegistrada && now - tempoInicioPresenca > 4000) {
      Serial.println("üìå Visita registrada do pet √† tigela de √°gua.");
      client.publish("petwater/visitas", "Visita registrada");
      visitaRegistrada = true;
    }
  } else {
    petPresente = false;
    visitaRegistrada = false;
  }

  delay(10);
}
